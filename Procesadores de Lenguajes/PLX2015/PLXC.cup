import java_cup.runtime.*;
import ctdGenerator.*;
import java.util.*;

action code {:
    boolean isCasting = false;
    String castingType = null;

    List<String> cast_list = new ArrayList<>();

    public String get_curret_cast(){
        if (cast_list.size() == 0){
            return null;
        }
        return cast_list.get(cast_list.size()-1);
    }

    public void remove_curret_cast(){
        cast_list.remove(cast_list.size()-1);
    }

:};

parser code {:
    public void syntax_error(Symbol cur_token){
		Global.error("syntax error");
	}

:};

//**********************************************
//      TERMINALES
//*********************************************
terminal String INT_NUM, FLOAT_NUM;       //numéricos
terminal PLUS, MINUS, MUL, DIV, UMINUS, MOD;   //operadores numéricos
terminal OB, CB, OCB, CCB, OCC, CCC;            //paréntesis
terminal PYC, COMMA;
terminal String VAR_NAME;   //nombre de variables
terminal PRINT;             //funciones predefinidas
terminal INT, FLOAT;               //tipos
terminal ASIG;
terminal LT, LE, GT, GE, EQ, NE, OR, AND, NOT;
terminal IF, ELSE;
terminal INC, DEC;
terminal FOR, TO, DOWNTO, DO, STEP,IN;
terminal WHILE;
terminal INT_CAST, FLOAT_CAST;

//**********************************************
//      NO TERMINALES
//**********************************************
non terminal sentence, list_sentences;
non terminal expression;
non terminal Expression  numeric_expression, exp; //expresiones numéricas
non terminal var_declaration, var_inicialization;               //delcaración de variables
non terminal List<String> var_list, vars_ini_array;        //lista de variables en la declaración (sin inicializar)
non terminal  int_var_list_declaration; //lista de variables para su inicialización
non terminal String p_value;
non terminal list_declaration;
non terminal Expression value_part, inicialization_part;
non terminal Condition condition;   //condiciones aritméticas


non terminal if_sentence;   //sentencia if
non terminal String if_head;
non terminal else_if_part;

non terminal for_sentence; //sentencias for
non terminal for_loop; //for estilo java
non terminal for_to_sent, for_down_sent, for_in_sent;
non terminal String for_head, down_label, to_label;
non terminal Expression loop_var, step_part, increment_part;
non terminal Var array_for_val;

non terminal while_sentence, do_while_sentence;
non terminal String while_head;
non terminal String do_head;

non terminal block;

non terminal Expression float_part;
non terminal float_var_list_declaration;

non terminal Expression cast_expression;
non terminal String cast_type;

non terminal int_array_declaration, float_array_declaration;
non terminal List array_init_declaration;
non terminal List int_array_value_part, float_array_value_part;
non terminal List array_value_part;

//**********************************************
//      PRECEDENCIAS
//**********************************************
precedence left ASIG;
precedence left OR;
precedence left AND;
precedence left EQ, NE;
precedence left LT, LE, GT, GE;
precedence left PLUS, MINUS;
precedence left MUL, DIV, MOD;
precedence right UMINUS, INC, DEC;
precedence right VAR_NAME;
precedence left INT_CAST, FLOAT_CAST;

precedence nonassoc IF, ELSE;

//**********************************************
//      GRAMÁTICA
//**********************************************
list_sentences ::=
    sentence
    |
    list_sentences sentence
    ;

sentence ::=
    expression PYC
    |
    if_sentence
    |
    for_sentence
    |
    while_sentence
    |
    do_while_sentence
    |
    PRINT OB p_value:p CB PYC 
    {: Print.printValue(p); :}
    |
    block
    |
    error 
    {:  parser.syntax_error(null);    :}
    ;

expression ::=
    exp
    |
    var_declaration
    |
    var_inicialization
    ;

p_value ::=
    exp:n
    {:  RESULT = n.getValue().toString();    :}
    ;

exp ::=
    numeric_expression:n 
    {: RESULT = n; :}
    |
    cast_type:t  {: cast_list.add(t); castingType = get_curret_cast(); :} 
    exp:n
    {: 
        RESULT = Operation.cast(n, castingType); 
        remove_curret_cast(); 
        castingType = get_curret_cast(); 
    :}
    ;

//**********************************************
//      EXPRESIONES NUMÉRICAS
//**********************************************
numeric_expression ::=
    OB exp:n CB
    {:   
        RESULT = n;
    :}
    |
    INT_NUM:n
    {:  
        RESULT = new Expression(Global.INTEGER, n);
    :}
    |
    FLOAT_NUM:f
    {:  
        RESULT = new Expression(Global.FLOAT, f);   
    :}
    |
    VAR_NAME:n
    {:   
        RESULT = VarDeclaration.getVar(n, null);
    :}
    |
    VAR_NAME:n OCC exp:i CCC
    {:
        RESULT = VarDeclaration.get_array_index_value(n, i.getValue().toString());
    :}
    |
    VAR_NAME:n OCC exp:i CCC ASIG exp:e 
    {:
        RESULT = VarDeclaration.reasign_array_index_value(n, i.getValue().toString(), e);
    :}
    |
    INC VAR_NAME:n
    {:  RESULT = VarDeclaration.varValueInc(n, Global.LEFT);    :}
    |
    DEC VAR_NAME:n
    {:  RESULT = VarDeclaration.varValueDec(n, Global.LEFT);    :}
    |
    VAR_NAME:n INC
    {:  RESULT = VarDeclaration.varValueInc(n, Global.RIGHT);    :}
    |
    VAR_NAME:n DEC
    {:  RESULT = VarDeclaration.varValueDec(n, Global.RIGHT);   :}
    |
    exp:e1 PLUS exp:e2 
    {:  RESULT = Operation.arithmetic(e1, Global.PLUS, e2, castingType);            :}
    |
    exp:e1 MINUS exp:e2
    {:  RESULT = Operation.arithmetic(e1, Global.MINUS, e2, castingType); :}
    |
    exp:e1 MUL exp:e2
    {:  RESULT = Operation.arithmetic(e1, Global.MUL, e2, castingType); :}
    |
    exp:e1 DIV exp:e2
    {:  RESULT = Operation.arithmetic(e1, Global.DIV, e2, castingType); :}
    |
    exp:e1 MOD exp:e2
    {:  RESULT = Operation.arithmetic(e1, Global.MOD, e2, castingType);    :}
    |
    MINUS exp:e
    {:   RESULT = Operation.arithmetic(null, Global.MINUS, e, castingType);    :}%prec UMINUS
    ;


cast_type ::=
    FLOAT_CAST 
    {:  RESULT = Global.FLOAT; :}
    |
    INT_CAST
    {:  RESULT = Global.INTEGER; :}
    ;

//**********************************************
//      CONDICIONES
//**********************************************
condition ::=
    OB condition:c CB
    {: RESULT = c; :}
    |
    exp:e1 EQ exp:e2
    {:   RESULT = Operation.condition(e1, Global.EQ, e2);   :}
    |
    exp:e1 NE exp:e2
    {:   RESULT = Operation.condition(e1, Global.NE, e2);   :}
    |
    exp:e1 LT exp:e2
    {:   RESULT = Operation.condition(e1, Global.LT, e2);   :}
    |
    exp:e1 LE exp:e2
    {:   RESULT = Operation.condition(e1, Global.LE, e2);   :}
    |
    exp:e1 GT exp:e2
    {:   RESULT = Operation.condition(e1, Global.GT, e2);   :}
    |
    exp:e1 GE exp:e2
    {:   RESULT = Operation.condition(e1, Global.GE, e2);   :}
    |
    NOT condition:c
    {:   RESULT = Operation.booleanOperator(null, Global.NOT, c);   :}
    |
    condition:c1 AND {: System.out.println(c1.true_label() + ":");    :}
    condition:c2 {: RESULT = Operation.booleanOperator(c1, Global.AND, c2); :}
    |
    condition:c1 OR {:  System.out.println(c1.false_label() + ":");   :}
    condition:c2 {: RESULT = Operation.booleanOperator(c1, Global.OR, c2);   :}
    ;


//**********************************************
//     DECLARACIÓN DE VARIABLES
//**********************************************
var_declaration ::=

    //inicialización de una lista de variables a un valor entero
    INT int_var_list_declaration 
    |
    INT int_array_declaration
    |
    FLOAT float_var_list_declaration
    |
    FLOAT float_array_declaration
    ;


/* Inicialización de una lista de variables a un valor*/
int_var_list_declaration ::=
   int_var_list_declaration:l COMMA VAR_NAME:n value_part:e
    {:  
        System.out.println("#DECLARACIÓN: " + n + " = " + e.getValue());
       VarDeclaration.var_declaration(n, e, Global.INTEGER);
    :}
    |
    VAR_NAME:n value_part:e
    {:
        System.out.println("#DECLARACIÓN: " + n + " = " + e.getValue());
        VarDeclaration.var_declaration(n, e, Global.INTEGER);
    :}
    ;

value_part ::=
    ASIG exp:e
    {: RESULT = e; :}
    |
    {: RESULT = new Expression(Global.INTEGER, new Integer(0)); :}
    ;

float_var_list_declaration ::=
    float_var_list_declaration:l COMMA VAR_NAME:n value_part:e
    {:  
       System.out.println("#DECLARACIÓN: " + n + " = " + e.getValue());
       VarDeclaration.var_declaration(n, e, Global.FLOAT);
    :}
    |
    VAR_NAME:n float_part:e
    {:
        System.out.println("#DECLARACIÓN: " + n + " = " + e.getValue());
        VarDeclaration.var_declaration(n, e, Global.FLOAT);
    :}
    ;

float_part ::=
    ASIG exp:e
    {: RESULT = e; :}
    |
    {: RESULT = new Expression(Global.FLOAT, new Float(0)); :}
    ;

/*  Declaración múltiple de variables (actualización de variables ya declaradas)  */
var_inicialization ::=
    vars_ini_array:l  ASIG inicialization_part:e
    {: 
        if (e.getType().equals(Global.LIST)){
            VarDeclaration.multipleAsigArrayVar(l, (List)e.getValue());
        }else {
            VarDeclaration.multipleAsigValueVar(l, e); 
        }
    :}
   ;

inicialization_part ::=
    array_value_part:l
    {: RESULT = new Expression(Global.LIST, l);  :}
    |
    exp:e
    {: 
        if (e.getType().equals(Global.ARRAY)){
            String varName = e.getValue().toString();
            Var v = TS.getVar(varName);
            if (v==null){
                Global.error("variable '" + varName + "' no declarada");
            }
            Array a = (Array)v.getValue();
            RESULT = new Expression(Global.LIST, a.getList()); 
        }else{
            RESULT = e;
        }
    :}
    |
    OCB array_init_declaration:ar CCB
    {:  RESULT = new Expression (Global.LIST, ar);              :}
    
    ;
    

array_value_part ::=
    ASIG OCB array_init_declaration:ar CCB
    {:  RESULT = ar;              :}
    |
    ASIG VAR_NAME:n 
    {:
        Var v = TS.getVar(n);
        if (v==null){
            Global.error("variable no declarada");
        }
        if (!v.getType().equals(Global.ARRAY)){
            Global.error("la variable no es un array");
        }
        Array ar = (Array)v.getValue();
        RESULT = (List)ar.getList();
    :}
    |
    {:  RESULT = new ArrayList<>(); :}
    ;


vars_ini_array ::=
    VAR_NAME:n
    {:  
        List<String> l = new ArrayList<>(); 
        l.add(n); 
        RESULT = l; 
    :}
    |
    vars_ini_array:v ASIG VAR_NAME:n
    {:
        List<String> l = new ArrayList<>();
        l.add(n);
        l.addAll(v);
        RESULT = l;
    :}
    ;

//**********************************************
//     DECLARACION DE ARRAYS
//**********************************************

/* Arrays de enteros     */
int_array_declaration ::=
    VAR_NAME:n OCC INT_NUM:i CCC array_value_part:v
    {: 
        Integer in = Integer.parseInt(i);
        int size = in.intValue();
        if (v.size() < 0 || (v.size()< size && v.size() > size)){
            Global.error("la longitud no coincide en la declaracion");
        }       
        Array array = new Array(v, i, Global.INTEGER);
        VarDeclaration.array_inicialization(n, array, Global.ARRAY, false);
    :}
    ;



/* Arrays de float */
float_array_declaration ::=
    VAR_NAME:n OCC INT_NUM:i CCC array_value_part:v
    {: 
        Integer in = Integer.parseInt(i);
        int size = in.intValue();
        if (v.size() < 0 || (v.size()< size && v.size() > size)){
            Global.error("la longitud no coincide en la declaracion");
        }       
        Array array = new Array(v, i, Global.FLOAT);
        VarDeclaration.array_inicialization(n, array, Global.ARRAY, false);
    :}
    ;



/* Declaracion de arrays */
array_init_declaration ::=
    array_init_declaration:a COMMA exp:e
    {:
        List res = new ArrayList<>();
        res.addAll(a);
        res.add(e);
        RESULT = res;
    :}
    |
    exp:e
    {:
        List res = new ArrayList<>();
        res.add(e);
        RESULT = res;
    :}
    ;
    

//**********************************************
//     SENTENCIA IF
//**********************************************
if_sentence ::=
    if_head:i OB condition:c CB {:  Global.label(c.true_label());    :}
    sentence                    {:  Global.gotoLabel(i); Global.label(c.false_label()); :}
    else_if_part                {:  Global.label(i); :}
    ;

if_head::=
    IF {:  RESULT = TS.newLabel(); :}
    ;

else_if_part ::=
    ELSE sentence                    {: String lab = TS.newLabel(); Global.label(lab);    :}
    |
    ;

//*********************************************************
//     SENTENCIA FOR 
//***********************************************************
for_sentence ::=
    for_to_sent
    |
    for_down_sent
    |
    for_loop
    |
    for_in_sent
    ;

for_head ::=
    FOR {: RESULT = TS.newLabel(); :}
    ;

//*********************************************************
//     SENTENCIA FOR ESTILO JAVA
//***********************************************************
for_loop ::=
    for_head:f OB loop_var:e1 PYC   {: Global.label(f); :}
    condition:c PYC                 {: RESULT = TS.newLabel(); Global.label(RESULT.toString());     :}
    increment_part:e2 CB                {: Global.gotoLabel(f); Global.label(c.true_label()); :}
    sentence                        {: Global.gotoLabel(RESULT.toString()); Global.label(c.false_label());    :}
    ;

increment_part ::=
    numeric_expression:n
    {:  RESULT = n;    :}
    |
    VAR_NAME:n ASIG exp:e
    {:   VarDeclaration.varValAct(n, e); RESULT = new Expression(e.getType(), n);   :}
    |
    var_inicialization:v
    {:   RESULT = (Expression)v;   :}
    ;

//*********************************************************
//     SENTENCIA FOR ESTILO PASCALL (for-to | for-downto)
//***********************************************************

for_to_sent ::=
    for_head:f loop_var:v to_label:t exp:e2  {: Global.label(f); RESULT = Operation.condition(v, Global.LE, e2); 
                                                               Global.label(t); :}
    step_part:s DO                                          {: ForLoop.loop_var(v, Global.PLUS,s); Global.gotoLabel(f);
                                                               Condition c = (Condition)RESULT;
                                                               Global.label(c.true_label()); :}
    sentence                                                {: Global.gotoLabel(t);
                                                               Condition c = (Condition)RESULT;
                                                               Global.label(c.false_label());
                                                            :}
    ;

for_down_sent ::=
    for_head:f loop_var:v down_label:d exp:e2{: Global.label(f); RESULT = Operation.condition(v, Global.GE, e2); 
                                                               Global.label(d); :}
    step_part:s DO                                          {: ForLoop.loop_var(v, Global.MINUS, s); Global.gotoLabel(f);
                                                               Condition c = (Condition)RESULT;
                                                               Global.label(c.true_label()); :}
    sentence                                                {: Global.gotoLabel(d);
                                                               Condition c = (Condition)RESULT;
                                                               Global.label(c.false_label());
                                                            :}
    ;

loop_var ::=
    VAR_NAME:n ASIG exp:e
    {:      
        String name = VarDeclaration.intVarInicialization(n, e);
        RESULT = new Expression(e.getType(), name);
    :}
    |
    VAR_NAME:n
    {:
        Var v = TS.getVar(n);
        if (v==null){
            Global.error("variable no declarada");
        }
        RESULT = new Expression (v.getType(), v.getName());
    :}
    |
    VAR_NAME:n OCC exp:i CCC 
    {:
        String index = i.getValue().toString();
        RESULT = VarDeclaration.get_array_index_value_for_in(n, index);
    :}
    ;

to_label ::=
    TO {:  RESULT = TS.newLabel(); :}
    ;

down_label ::=
    DOWNTO {:  RESULT = TS.newLabel();  :}
    ;

step_part ::=
    STEP exp:n
    {:  RESULT = n;    :}
    |
    {:  RESULT = new Expression(Global.INTEGER, new Integer(1));    :}
    ;

//**********************************************
//      SENTENCIA FOR IN
//**********************************************
for_in_sent ::=
    for_head:f loop_var:l IN array_for_val:array
            {: 
                ForInLoop fi = new ForInLoop(f, array, l);
                RESULT = fi;
            :}
    DO      {:
                ForInLoop fi = (ForInLoop)RESULT;
                fi.init_body();
                RESULT = fi;
            :}
    sentence {:
                ForInLoop fi = (ForInLoop)RESULT;
                fi.end_body();
                fi.exit_label();
            :}
    ;

array_for_val ::=
    VAR_NAME:n
    {:
        Var v = TS.getVar(n);
        if (v==null){
            Global.error("variable no declarada");
        }
        if (!v.getType().equals(Global.ARRAY)){
            Global.error("la variable '" + n + "' no es un array");
        }
        Array ar = (Array)v.getValue();
        RESULT = new Var(v.getName(), ar.getType(), ar.getList());
    :}
    |
    OCB array_init_declaration:ar CCB
    {:
        if (!VarDeclaration.same_array_type(ar)){
            Global.error("array no valido (distintos tipos)");
        }

        String type = VarDeclaration.type_of(ar.get(0));
        String temp = TS.newTempVar();
        ForInLoop.init_array_declaration(temp, ar);
        RESULT = new Var(temp, type, ar);
    :}
    ;

//**********************************************
//      SENTENCIA WHILE
//**********************************************

while_sentence ::=
    while_head:w        {:  Global.label(w);    :}
    OB condition:c      {:  Global.label(c.true_label()); :}
    CB sentence         {:  Global.gotoLabel(w); Global.label(c.false_label()); :}
    ;

while_head ::=
    WHILE {:  RESULT = TS.newLabel(); :}
    ;


//**********************************************
//      SENTENCIA DO-WHILE
//**********************************************
do_while_sentence ::=
    do_head:d                            {:  Global.label(d);    :}
    sentence WHILE OB condition:c        {:  Global.label(c.true_label()); 
                                             Global.gotoLabel(d);   
                                             Global.label(c.false_label()); :}
    CB PYC
    ;

do_head ::=
    DO {:  RESULT = TS.newLabel(); :}
    ;


//**********************************************
//     CONTROL DE BLOQUES
//**********************************************
block ::=
    OCB             {: TS.newBlock(); :}
    list_sentences  {: TS.deleteBlock(); :}
    CCB 
    ;